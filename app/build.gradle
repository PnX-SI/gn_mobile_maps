plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.dagger.hilt.android)
    alias(libs.plugins.kapt)
}

version = "0.8.0"

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId "fr.geonature.maps.sample"
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 34
        versionCode updateVersionCode(project.name)
        versionName version
        buildConfigField "String", "BUILD_DATE", "\"" + new Date().getTime() + "\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = project.name + "-" + versionName
    }

    buildTypes {
        debug {
            versionNameSuffix ".${defaultConfig.versionCode}-DEV"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    namespace 'fr.geonature.maps.sample'
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':mountpoint')
    implementation project(':maps')

    implementation libs.androidx.appcompat
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.work.runtime.ktx
    implementation libs.material

    // Hilt dependencies
    implementation libs.androidx.hilt.work
    implementation libs.hilt.android
    kapt libs.androidx.hilt.compiler
    kapt libs.hilt.android.compiler

    // Logging
    implementation libs.tinylog.api.kotlin
    runtimeOnly libs.tinylog.impl

    // Testing dependencies
    testImplementation libs.androidx.core
    testImplementation libs.junit
    testImplementation libs.robolectric
}
